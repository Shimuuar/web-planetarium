{-# LANGUAGE JavaScriptFFI     #-}
{-# LANGUAGE OverloadedStrings #-}
-- |
-- FRP helpers for writing code
module JavaScript.FRP (
    -- * FRP
    runNowMaster'
  , actimate
  , actimateB
    -- * Input streams
  , onClickStream
  , innerSizeBehavior
  , wheelEventStream
  , numberStream
  , streamSelectInput
    -- ** Compounds
  , adjustStream
  ) where

import Control.Applicative
import Control.Monad
import Control.FRPNow
import Control.Concurrent (threadDelay)

import Data.Monoid
import GHCJS.Types
import GHCJS.Foreign
import GHCJS.Marshal



----------------------------------------------------------------
-- High level functions
----------------------------------------------------------------

-- | Execute Now computation
--
--   'runNowMaster' doesn't work
runNowMaster' :: Now () -> IO a
runNowMaster' now = do
  initNow void (now >> return never)
  forever $ threadDelay maxBound

actimate :: (Eq a) => (a -> IO ()) -> Behavior a -> Now ()
actimate fun bhv = do
  sync . fun =<< sample bhv
  callIOStream fun $ toChanges bhv

actimateB :: (Eq a) => (b -> a -> IO ()) -> Behavior b -> Behavior a -> Now ()
actimateB fun bhvB bhv = do
  b <- sample bhvB
  sync . fun b =<< sample bhv
  callIOStream id $ (fun <$> bhvB) <@@> toChanges bhv

----------------------------------------------------------------
-- Input streams
----------------------------------------------------------------

-- | Stream of onclick events
onClickStream
  :: JSString                   -- ^ JQuery selector
  -> Now (EvStream ())
onClickStream selector = do
  -- Create event stream
  (stream,call) <- callbackStream
  -- Bind callback to JS
  jsCall <- sync $ syncCallback NeverRetain True $ call ()
  sync $ jq_event_click selector jsCall
  -- Done
  return stream

-- | Behaviour of value
innerSizeBehavior
  :: JSString                   -- ^ JQuery selector
  -> Now (Behavior (Int,Int))
innerSizeBehavior selector = do
  (stream,call) <- callbackStream
  let size = do
        h <- jq_innerHeight selector
        w <- jq_innerWidth selector
        return (w,h)
  jsCall <- sync $ syncCallback NeverRetain True $ call =<< size
  wh     <- sync size
  sync $ jq_event_resize jsCall
  sample $ foldrSwitch (pure wh) (pure <$> stream)

-- | Stream for wheel events
wheelEventStream :: JSString -> Now (EvStream Double)
wheelEventStream sel = do
  (stream,call) <- callbackStream
  jsCall <- sync $ syncCallback1 AlwaysRetain True $ \i -> do
    Just ii <- fromJSRef i
    call ii
  sync $ jq_wheel sel jsCall
  return stream

-- | Stream of changes generated by select control
streamSelectInput
  :: [(String,a)]               -- ^ List of options
  -> JSString                   -- ^ JQuery selector
  -> Now (EvStream a)
streamSelectInput [] _   = return mempty
streamSelectInput as sel = do
  -- Append options
  sync $ forM_ (as `zip` [0..]) $ \((nm,_),i) ->
    jq_append_option sel i (toJSString nm)
  -- Add callback
  (stream,call) <- callbackStream
  jsCall <- sync $ syncCallback AlwaysRetain True $ do
    n <- jq_val sel
    call $ snd $ as !! read (fromJSString n)
  sync $ jq_event_change sel jsCall
  return stream


numberStream :: JSString -> Double -> (Double,Double) -> Now (EvStream Double, Behavior Double)
numberStream sel x0 (minX,maxX) = do
  (stream,call) <- callbackStream
  -- Callbacks
  jsCall <- sync $ syncCallback1 AlwaysRetain True $ \xRef -> do
    Just x <- fromJSRef xRef
    call x
  -- Bind handler
  sync $ js_evts_numeric sel jsCall minX maxX x0
  bhv <- sample $ fromChanges x0 stream
  return (stream, bhv)



----------------------------------------------------------------
-- Compound functions
----------------------------------------------------------------

adjustStream
  :: Num aa
  => (JSString, a -> a)    -- ^ Decrement
  -> (JSString, a -> a)    -- ^ Increent
  -> Now (EvStream (a -> a))
adjustStream (down,funD) (up,funU) = do
  streamUp   <- onClickStream up
  streamDown <- onClickStream down
  return $ (funU <$ streamUp)
        <> (funD <$ streamDown)


----------------------------------------------------------------
-- FFI
----------------------------------------------------------------

foreign import javascript safe "$( $1 ).click( $2 )"
  jq_event_click :: JSString -> JSFun (IO ()) -> IO ()

foreign import javascript safe "$(window).resize( $1 )"
  jq_event_resize :: JSFun (IO ()) -> IO ()

foreign import javascript safe "$( $1 ).change($2)"
  jq_event_change :: JSString -> JSFun (IO ()) -> IO ()


foreign import javascript safe "$( $1 ).innerHeight()"
  jq_innerHeight :: JSString -> IO Int
foreign import javascript safe "$( $1 ).innerWidth()"
  jq_innerWidth :: JSString -> IO Int

foreign import javascript safe "$( $1 ).val()"
  jq_val :: JSString -> IO JSString

foreign import javascript safe
  "$( $1 ).bind('DOMMouseScroll mousewheel', function(e){\
  \  var d=e.originalEvent.detail; \
  \  if(Math.abs(d) > 0.5 ) { $2(d); }})"
  jq_wheel :: JSString -> JSFun (JSRef Double -> IO ()) -> IO ()

foreign import javascript safe "$( $1 ).append('<option value=\"'+$2+'\">'+$3+'</option>')"
  jq_append_option :: JSString -> Int -> JSString -> IO ()

foreign import javascript safe "evts_numeric($1,$2,$3,$4,$5)"
  js_evts_numeric :: JSString
                  -> JSFun (JSRef Double -> IO ())
                  -> Double -> Double -> Double -> IO ()
